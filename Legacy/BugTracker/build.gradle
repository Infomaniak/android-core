plugins {
    id 'com.android.library'
    id 'kotlin-android'
    alias(core.plugins.navigation.safeargs)
}

def legacyCompileSdk = rootProject.ext["legacyCompileSdk"]
def legacyMinSdk = rootProject.ext["legacyMinSdk"]
def javaVersion = rootProject.ext["javaVersion"]

android {
    namespace 'com.infomaniak.core.legacy.bugtracker'
    compileSdk legacyCompileSdk

    defaultConfig {
        minSdk legacyMinSdk
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions { jvmTarget = javaVersion }

    buildFeatures { viewBinding true }
}

dependencies {
    implementation("com.infomaniak.core:AppVersionChecker")
    implementation("com.infomaniak.core:Auth")
    // To access API URL
    // New Core via composite build: will be substituted to the included build project
    implementation("com.infomaniak.core:Network")

    implementation project(':Core:Legacy')

    implementation("com.infomaniak.core:Ui")
    implementation("com.infomaniak.core:Ui:View")
}

// Workaround for Gradle/AGP strict task dependency validation.
// Since Core is now included as a composite build while Legacy is still part of the main build,
// LintModelWriterTask may read outputs from :Core:prepareLintJarForPublish without an explicit
// dependency, which causes CI failures with Gradle 8+.
//
// This enforces the correct task execution order and keeps the build deterministic.
//
// TODO[AGP9-Core]: Remove this workaround once Legacy is migrated to the new Core,
//  or re-evaluate after upgrading to AGP 9 if Legacy is still in use.
tasks.matching { it.name == "generateDebugLintModel" }.configureEach {
    dependsOn(":Core:prepareLintJarForPublish")
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    alias(core.plugins.navigation.safeargs)
}

def legacyCompileSdk = rootProject.ext["legacyCompileSdk"]
def legacyMinSdk = rootProject.ext["legacyMinSdk"]
def javaVersion = rootProject.ext["javaVersion"]

android {
    namespace 'com.infomaniak.core.legacy.stores'
    compileSdk legacyCompileSdk

    defaultConfig {
        minSdk legacyMinSdk
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions { jvmTarget = javaVersion }

    buildFeatures { viewBinding true }

    flavorDimensions += 'distribution'
    productFlavors {
        standard { getIsDefault().set(true) }
        fdroid
    }
}

dependencies {
    def appVersionCheckerProject = findProject(":AppVersionChecker")
    if (appVersionCheckerProject == null) {
        implementation("com.infomaniak.core:AppVersionChecker")
    } else {
        implementation(appVersionCheckerProject)
    }

    // To access API URL
    def networkProject = findProject(":Network")
    if (networkProject != null) {
        implementation(networkProject)
    } else {
        // New Core via composite build: will be substituted to the included build project
        implementation("com.infomaniak.core:Network")
    }

    implementation project(':Core:Legacy')

    api core.androidx.datastore.preferences

    implementation core.androidx.work.runtime.ktx

    implementation core.androidx.concurrent.futures.ktx

    standardImplementation core.play.review
    standardImplementation core.play.review.ktx

    standardImplementation core.app.update
    standardImplementation core.app.update.ktx
}

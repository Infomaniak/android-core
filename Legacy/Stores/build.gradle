apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'

def legacyCompileSdk = rootProject.ext["legacyCompileSdk"]
def legacyMinSdk = rootProject.ext["legacyMinSdk"]
def javaVersion = rootProject.ext["javaVersion"]

android {
    namespace 'com.infomaniak.lib.stores'
    compileSdk legacyCompileSdk

    defaultConfig {
        minSdk legacyMinSdk
        targetSdk legacyCompileSdk
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions { jvmTarget = javaVersion }

    buildFeatures { viewBinding true }

    flavorDimensions += 'distribution'
    productFlavors {
        standard { getIsDefault().set(true) }
        fdroid
    }
}

dependencies {
    implementation project(':Core:Legacy')

    // Version 1.1.5 has some issues so we have to use the alpha version for now
    api 'androidx.datastore:datastore-preferences:1.2.0-alpha02'

    implementation 'androidx.work:work-runtime-ktx:2.9.1' // Doesn't build when bumped to 2.10.0 (waiting for Api 35)
    implementation 'androidx.concurrent:concurrent-futures-ktx:1.2.0'

    def appReviewVersion = '2.0.2'
    standardImplementation "com.google.android.play:review:$appReviewVersion"
    standardImplementation "com.google.android.play:review-ktx:$appReviewVersion"

    def appUpdateVersion = "2.1.0"
    standardImplementation "com.google.android.play:app-update:$appUpdateVersion"
    standardImplementation "com.google.android.play:app-update-ktx:$appUpdateVersion"
}

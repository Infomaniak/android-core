plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
    id 'com.google.devtools.ksp'
    alias(core.plugins.navigation.safeargs)
}

rootProject.ext {
    legacyCompileSdk = 35
    legacyMinSdk = 27
}

def legacyCompileSdk = rootProject.ext["legacyCompileSdk"]
def legacyMinSdk = rootProject.ext["legacyMinSdk"]
def javaVersion = rootProject.ext["javaVersion"]

android {
    namespace 'com.infomaniak.core.legacy'
    compileSdk legacyCompileSdk

    defaultConfig {
        minSdk legacyMinSdk

        testOptions.unitTests.includeAndroidResources = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp { arg("room.schemaLocation", "$projectDir/schemas") }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    kotlinOptions { jvmTarget = javaVersion }
}

dependencies {
    // To access API URL
    def networkProject = findProject(":Network")
    if (networkProject != null) {
        implementation(networkProject)
    } else {
        // New Core via composite build: will be substituted to the included build project
        implementation("com.infomaniak.core:Network")
    }

    api core.kotlinx.coroutines.android
    api core.kotlinx.coroutines.test
    api core.kotlinx.serialization.json

    api core.navigation.fragment.ktx
    api core.navigation.ui.ktx

    api core.constraintlayout
    api core.coordinatorlayout
    api core.androidx.core.ktx
    api core.androidx.lifecycle.livedata.ktx
    api core.androidx.recyclerview
    api core.swiperefreshlayout

    api core.appcompat
    api core.appcompat.resources

    implementation core.splitties.appctx

    api core.room.ktx
    api core.room.runtime
    ksp core.room.compiler

    api core.material
    api core.gson

    api core.coil.two

    api core.android.login
    api core.stetho.okhttp3
    api core.okhttp

    api core.sentry.android
    api core.sentry.okhttp

    implementation core.progress.button

    api core.matomo

    testImplementation core.junit
    testImplementation core.androidx.junit
    testImplementation core.androidx.test.core
    testImplementation core.androidx.test.core.ktx
    testImplementation core.robolectric
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlinx-serialization'

android {
    namespace 'com.infomaniak.lib.core'

    rootProject.ext {
        coreMinSdk = 21
        coreTargetSdk = 33
        javaVersion = JavaVersion.VERSION_17
    }

    compileSdkVersion rootProject.ext.coreTargetSdk

    defaultConfig {
        minSdkVersion rootProject.ext.coreMinSdk
        targetSdkVersion rootProject.ext.coreTargetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'AUTOLOG_URL', '"https://manager.infomaniak.com/v3/mobile_login"'
        buildConfigField 'String', 'LOGIN_ENDPOINT_URL', '"https://login.infomaniak.com/"'
        buildConfigField 'String', 'INFOMANIAK_API', '"https://api.infomaniak.com/2/"'
        buildConfigField 'String', 'MATOMO_URL', '"https://analytics.infomaniak.com/matomo.php"'

        kapt { arguments { arg("room.schemaLocation", "$projectDir/schemas") } }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }

    buildFeatures { viewBinding true }

    kotlinOptions { jvmTarget = rootProject.ext.javaVersion }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.21'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'
    api 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1'

    def navigationVersion = '2.5.3'
    api "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    api "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    api 'androidx.core:core-ktx:1.10.1'
    api 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    api 'androidx.recyclerview:recyclerview:1.3.0'
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    def appcompatVersion = '1.6.1'
    api "androidx.appcompat:appcompat:$appcompatVersion"
    api "androidx.appcompat:appcompat-resources:$appcompatVersion"

    def roomVersion = '2.5.1'
    api "androidx.room:room-ktx:$roomVersion"
    api "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    api 'com.google.android.material:material:1.9.0'
    api 'com.google.code.gson:gson:2.10.1'

    def coilVersion = '2.4.0'
    api "io.coil-kt:coil-gif:$coilVersion"
    api "io.coil-kt:coil:$coilVersion"

    api 'com.facebook.stetho:stetho-okhttp3:1.6.0'
    api 'com.github.Infomaniak:android-login:2.7.0'
    api 'com.squareup.okhttp3:okhttp:4.11.0'
    api 'io.sentry:sentry-android:6.20.0'

    implementation 'com.github.razir.progressbutton:progressbutton:2.1.0'

    api 'com.github.matomo-org:matomo-sdk-android:4.1.4'
}
